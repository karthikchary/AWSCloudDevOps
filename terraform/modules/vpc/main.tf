resource "aws_vpc" "eks_vpc" {
    cidr_block = var.vpc_cidr
    enable_dns_hostnames = true
    enable_dns_support = true

    tags = {
      Name = "eks-vpc-vpc"
    }
}

resource "aws_internet_gateway" "igw" {
    vpc_id = aws_vpc.eks_vpc.id
    tags = {
      Name = "eks-vpc-igw"
    }
  
}

resource "aws_subnet" "public" {
    count = length(var.public_subnets)
    vpc_id = aws_vpc.eks_vpc.id
    cidr_block = var.public_subnets[count.index]
    availability_zone = var.availability_zones[count.index]
    map_public_ip_on_launch = true

    tags = {
     Name = "public-${count.index + 1}"
    }
}

resource "aws_subnet" "private" {
    count = length(var.private_subnets)
    vpc_id = aws_vpc.eks_vpc.id
    cidr_block = var.private_subnets[count.index]
    availability_zone = var.availability_zones[count.index]

    tags = {
      Name = "private-${count.index + 1}"
    }
}

resource "aws_eip" "nat" {
    domain = "vpc"
    tags = {
     Name = "nat-eip" 
    }
}

resource "aws_nat_gateway" "natgw" {
    allocation_id = aws_eip.nat.id
    subnet_id = aws_subnet.public[0].id
    tags = {
      Name = "eks-vpc-natgw"
    }
}

resource "aws_route_table" "public_rt" {
    vpc_id = aws_vpc.eks_vpc.id
    route {
        cidr_block = "0.0.0.0/0"
        gateway_id = aws_internet_gateway.igw.id
    }
    tags = {
        Name = "public-rt"
    }
}

resource "aws_route_table_association" "public_rta" {
    count = length(aws_subnet.public)
    subnet_id = aws_subnet.public[count.index].id
    route_table_id = aws_route_table.public_rt.id
}

resource "aws_route_table" "private_rt" {
    vpc_id = aws_vpc.eks_vpc.id
    route {
        cidr_block = "0.0.0.0/0"
        nat_gateway_id = aws_nat_gateway.natgw.id
    }
    tags = {
     Name = "private-rt" 
    }
}

resource "aws_route_table_association" "private_rta" {
    count = length(aws_subnet.private)
    subnet_id = aws_subnet.private[count.index].id
    route_table_id = aws_route_table.private_rt.id
}